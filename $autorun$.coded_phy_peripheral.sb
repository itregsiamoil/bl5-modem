//******************************************************************************
// Definitions
//******************************************************************************
#define DeviceName                    	"BL654"
#define BLE_TX_POWER                    8		// Possible values are 8, 7, 6, 5, 4, 3, 2, 0, -4, -8, -12, -16, -20, -40dBm
#define BLE_1M_PHY                      1
#define BLE_2M_PHY                      2
#define BLE_CODED_PHY                   4
#define ADV_CONN						0
#define ADV_DIRECT_CONN					1
#define ADV_EXT_CONN					6
#define ADV_EXT_DIRECT_CONN				7
#define ADV_TYPE						ADV_EXT_CONN
#define BLE_PHY							BLE_CODED_PHY
#define U_BUF							2048
#define U_SPEED							115200
#define EncryptConn						2 //2-encr, 1-open
#define AddAppearanceToAdv				0 //0\1
#define BleAppearance					3200	//3200-весы, 520-тэг, 128-компьютер, 64-телефонб 384-удаленное кправление, ble_types.h in SDK
#define DevNameLength					10
// Definitions for attribute values to achieve higher bandwidth needed for CODED PHY
#define USE_STRINT_IN_EVENTS_KEY_ID		213		// CFG ID 213
#define ENABLE_HIGH_BANDWIDTH_KEY_ID	214		// CFG ID 214
#define USE_STRING_IN_EVENTS			1		// 0: use EVATTRNOTIFY, 1: use EVATTRNOTIFYEX (See user manual)
#define BLE_ENABLE_HIGH_BANDWIDTH		1		// 0: Normal Bandwidth (default), 1: High Bandwidth					
#define BASEUUID						0x6340 //"\56\9a\11\01\b8\7f\49\0c\92\cb\11\ba\5e\a5\16\7c" VSP base UUID
#define RXUUID							0x6341
#define TXUUID							0x6342
dim URI$:URI$=							"162f2f7369616d6f696c2e7275" //http://siamoil.ru (UTF-8)
dim LESupFutr$:LESupFutr$=				"0018" //coded phy+adv ext (v6,pB,4.6 bt core specification)
#set $cmpif,0xFF

//******************************************************************************
// Global Variable Declarations
//******************************************************************************
dim rc                              	// Result code variable (used for checking if a function completed successfully or not)
dim BaseUUID                        	// Base UUID for the VSP service which other UUIDs will be siblings of
dim svcID                           	// VSP Service ID
dim TXchar                          	// TX characteristic
dim RXchar                          	// RX characteristic
dim ConnectionID:ConnectionID = 0		// Holds the connection ID of the VSP client (if one is connected)
dim UARTBlocked : UARTBlocked = 0   	// When set to 1 will not read data from the UART
dim BufferState : BufferState = 0		// Status flag show availible to transmit data
dim nAttributeMTU : nAttributeMTU = 23				//Default Maximum Transmission Unit on BL654
dim nAttributeLength : nAttributeLength = 20		//Default Attribute Data Length on BL654
dim nBleMaxPacketLength : nBleMaxPacketLength = 27	//Default maximum packet length on BL654
dim UartData$
dim DevName$ : DevName$ = DeviceName

//------------------------------------------------------------------------------
// Assert to check that a resultCode is 0 which means success
//------------------------------------------------------------------------------
sub AssertRC(rc, tag)
    if (rc != 0) then
        print "Assert Error ";integer.h'rc;" at tag ";tag;"\r\n"
    endif
endsub

//-------------------------------------------------------------------------------
// This function is to enable the High Bandwidth configuration in order to achieve the long range
//-------------------------------------------------------------------------------
FUNCTION EnableHighBandwidth()
	dim HighBandwidth,StringEvent
	rc = NvCfgKeyGet(USE_STRINT_IN_EVENTS_KEY_ID, StringEvent) // Get High bandwidth value
	IF  (StringEvent != USE_STRING_IN_EVENTS) THEN
		// Allow data to be thrown in messages (i.e. Use EVATTRNOTIFYEX instead of EVATTRNOTIFY)
		rc = NvCfgKeySet(USE_STRINT_IN_EVENTS_KEY_ID, USE_STRING_IN_EVENTS)
		Reset(0)
    ENDIF
    rc = NvCfgKeyGet(ENABLE_HIGH_BANDWIDTH_KEY_ID, HighBandwidth) // Get High bandwidth value
    IF  (HighBandwidth != BLE_ENABLE_HIGH_BANDWIDTH) THEN
        // Change the bandwidth configuration to HIGH (6 packets per interval)
        rc = NvCfgKeySet(ENABLE_HIGH_BANDWIDTH_KEY_ID, BLE_ENABLE_HIGH_BANDWIDTH)
        // Reset the module so that the data is overwritten  
		Reset(0)		
    ENDIF
ENDFUNC 0

//------------------------------------------------------------------------------
// Function that will create a custom VSP service with Tx/Rx characteristics
//------------------------------------------------------------------------------
function CreateVSPService()
    dim UUIDStr$,mdCccd,mdVal,s$,RxUUID,TxUUID,svcID,mdrxDesc,rxDesc$,mdtxDesc,txDesc$
    s$ = "\00" //Default value for characteristics  
    //Create the VSP service
	BaseUUID = BleHandleUuid16(BASEUUID)
	// UUIDStr$=BASEUUID
	// BaseUUID = BleHandleUuid128(UUIDStr$)
    rc = BleServiceNew(1, BaseUUID, svcID) : AssertRC(rc,21)
    //Create the RX characteristic
    mdVal = BleAttrMetadata(0,1,nAttributeLength,1,rc)
    RxUUID = RXUUID
    RxUUID = BleHandleUuidSibling(BaseUUID, RxUUID)
    rc = BleCharNew(8, RxUUID, mdVal, 0, 0) : AssertRC(rc,22)
	mdrxDesc=BleAttrMetadata(1,0,nAttributeLength,1,rc)
	rxDesc$="RX data"
	rc=BleCharDescUserDesc(rxDesc$,mdrxDesc)
    rc = BleCharCommit(svcID, s$, RXchar) : AssertRC(rc,23)
    //Create the TX characteristic
    mdVal = BleAttrMetadata(0,0,nAttributeLength,1,rc)
    mdCccd = BleAttrMetadata(1,1,2,0,rc) 
    TxUUID = TXUUID
    TxUUID = BleHandleUuidSibling(BaseUUID, TxUUID)
    rc = BleCharNew(16, TxUUID, mdVal, mdCccd, 0) : AssertRC(rc,24)
	mdtxDesc=BleAttrMetadata(1,0,nAttributeLength,1,rc)
	txDesc$="TX data"
	rc=BleCharDescUserDesc(txDesc$,mdtxDesc)
    rc = BleCharCommit(svcID, s$, TXchar) : AssertRC(rc,25)
    //Commit the service
    rc = BleServiceCommit(svcID) : AssertRC(rc,26)
endfunc 0

//------------------------------------------------------------------------------
// Start LE Adverts
//------------------------------------------------------------------------------
FUNCTION BleStartAdverts()
    DIM nMinConnInterval, nMaxConnInterval
	dim scRpt$, adRpt$, UUIDStr$	
	// Register GAP service with device name and connection intervals
    rc = BleGapSvcInit(DevName$, 0, BleAppearance, 7500, 10000, 4000000, 0) : AssertRC(rc,1)
	//Initialise the Advert report (no appearance AD)
    rc = BleAdvRptInit(adRpt$, 6, AddAppearanceToAdv, DevNameLength) : AssertRC(rc,2)
    //Init the scan report for the VSP service 
    rc = BleScanRptInit(scRpt$) : AssertRC(rc,3)
    //Advertise the 128 bit uuid in a scan report
    //rc = BleAdvRptAddUuid128(scRpt$, BaseUUID) : AssertRC(rc,4)
	rc = BleAdvRptAddUuid16(scRpt$, BaseUUID,-1,-1,-1,-1,-1) : AssertRC(rc,4)
	dim text1$:text1$=strdehexize$(LESupFutr$)
	rc=BleAdvRptAppendAD(adRpt$,0x27,text1$) : AssertRC(rc,5)
	dim text2$:text2$=strdehexize$(URI$)
	rc=BleAdvRptAppendAD(adRpt$,0x24,text2$) : AssertRC(rc,6)
    //Commit the advert report
    rc = BleAdvRptsCommit(adRpt$,scRpt$) : AssertRC(rc,7)
    // Start Ble adverts
    dim Addr$ : Addr$ = " "
    rc = BleAdvertStart(ADV_TYPE, Addr$, 100, 0, 0) : AssertRC(rc,8)
ENDFUNC 0

//------------------------------------------------------------------------------
// EVUARTRX - Function handler for UART data receive
//------------------------------------------------------------------------------
FUNCTION HndlrUartRx()
    dim RdLen, DataToSend$
	RdLen = UARTREADN(UartData$,2048)
	if ConnectionID!=0 then
		IF RdLen > 0 THEN
			IF BufferState == 0 THEN		
				#cmpif 0xFF : print "\nubuf\n"//=";strhexize$(UartData$);"\n"
				DataToSend$ = Left$(UartData$, nAttributeLength)
				rc = BLECHARVALUENOTIFY(TXchar, DataToSend$)
				select rc
					case 0
						rc=strlen(DataToSend$)
						StrShiftLeft(UartData$,rc)
						#cmpif 0xFF : print "-Succs send to char:";TXchar;"\n"
					case else
						BufferState = 1
						#cmpif 0xFF : print "-Ble send error! rc=";integer.h'rc;" msg-";DataToSend$;"\n"
				endselect
			endif
		ENDIF
	else 
		print "rcv-";UartData$;"\n"
		UartData$=""
	endif
ENDFUNC 1

//------------------------------------------------------------------------------
// EVNOTIFYBUF - Notification buffer was emptied
//------------------------------------------------------------------------------
FUNCTION HndlrNtfyBuf()
    IF BufferState == 1 THEN
        BufferState = 0
        rc=HndlrUartRx()
    ELSE
        rc=HndlrUartRx()
    ENDIF
	#cmpif 0xFF : print "Client: data rcv &  buf empty\n"
ENDFUNC 1

//------------------------------------------------------------------------------
// EVCHARVALUE - Handler for characteristic value written
//------------------------------------------------------------------------------
FUNCTION HndlrCharValue(nConnHandle, charHandle, offset, len, BYVAL Rd$ AS STRING)
    // Just print the data that we got from the char
	print "charvalue-";charHandle;"\n"
    IF charHandle == RXchar THEN
		PRINT Rd$
		#cmpif 0xFF : UartData$=Rd$
		#cmpif 0xFF : rc=HndlrUartRx()		
    ENDIF
ENDFUNC 1

//==============================================================================
// EVBLE_PHY_UPDATED - This handler is called when the BLE PHY is changed
//==============================================================================
function  HandlerPhyChngd(BYVAL hConn, BYVAL nStatus, BYVAL PhyTx, BYVAL PhyRx)
    print "-PHY Changed-Status: ";integer.h' nStatus;" - PhyTx=";PhyTx;" PhyRx =";PhyRx;"\n"
endfunc 1

//==============================================================================
// EVBLE_PHY_REQUEST - This handler is called when the BLE PHY is changed
//==============================================================================
function  HandlerPhyReq(BYVAL hConn, BYVAL PhyTx, BYVAL PhyRx)
    print "-PHY Request-PhyTx=";PhyTx;" PhyRx =";PhyRx;"\n"
    // Accept the PHY requested by the remote device
    rc = BlePhySet(hConn, PhyTx, PhyRx, 0)
endfunc 1

//------------------------------------------------------------------------------
// This shows the current connection parameters
//------------------------------------------------------------------------------
SUB ShowConnParms()
    DIM intrvl,sprvto,slat
    rc = BleGetCurConnParms(ConnectionID,intrvl,sprvto,slat)
    #cmpif 0xFF : print "-ConnParms-Interval=";intrvl;",SuperTout=";sprvto;",SlaveLatency=";slat;"\n"
ENDSUB

//------------------------------------------------------------------------------
// Function to handle Ble event messages
//------------------------------------------------------------------------------
#define BLE_EVBLEMSGID_CONNECT                  0   //nCtx = connection handle
#define BLE_EVBLEMSGID_DISCONNECT               1   //nCtx = connection handle
#define BLE_EVBLEMSGID_CONN_PARMS_UPDATE        14  //nCtx = connection handle
//------------------------------------------------------------------------------
function HndlrBleMsg(nMsgId, nCtx)
    select nMsgId
    case BLE_EVBLEMSGID_CONNECT
        PRINT "-Connected\n"
        ConnectionID = nCtx
        ShowConnParms()
    case BLE_EVBLEMSGID_DISCONNECT
        PRINT "-Disconnected!\n"
        dim Addr$ : Addr$ = ""
        rc = BleAdvertStart(ADV_TYPE, Addr$, 100, 0, 0)
		ConnectionID = 0
    case BLE_EVBLEMSGID_CONN_PARMS_UPDATE
        ShowConnParms()
    case else
    endselect
endfunc 1

//******************************************************************************
// main()
//******************************************************************************
UARTClose()
rc = UartOpen(U_SPEED, U_BUF, U_BUF, "CN81H")
rc = BleTxPowerSet(BLE_TX_POWER)
rc = EnableHighBandwidth()
rc = CreateVSPService()
rc = BleAdvertConfig(4,BLE_PHY)
rc = BleAdvertConfig(5,BLE_PHY)
rc = BleStartAdverts()
dim addr$:addr$ = SYSINFO$(4)
print "Hi! My address is "; strhexize$(addr$);"\n"

//******************************************************************************
//Setup event handlers
//******************************************************************************
// UART events
ONEVENT EVUARTRX			CALL HndlrUartRx
// Generic BLE events
ONEVENT EVBLEMSG			CALL HndlrBleMsg
ONEVENT EVCHARVALUE			CALL HndlrCharValue         // Used if AT+CFG 213=1
ONEVENT EVNOTIFYBUF			CALL HndlrNtfyBuf
// BLE PHY
OnEvent EVBLE_PHY_UPDATED	CALL HandlerPhyChngd
OnEvent EVBLE_PHY_REQUEST	CALL HandlerPhyReq

WAITEVENT
